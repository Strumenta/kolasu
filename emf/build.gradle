plugins {
    id 'org.jetbrains.kotlin.jvm'
    id "org.jlleitschuh.gradle.ktlint"
    id("maven-publish")
    id("idea")
    id("signing")
    id("org.jetbrains.dokka")
    id "antlr"
}

java {
    sourceCompatibility = "$jvm_version"
    targetCompatibility = "$jvm_version"
    registerFeature('cli') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    antlr "org.antlr:antlr4:$antlr_version"
    api project(":core")
    api group: 'org.eclipse.emf', name: 'org.eclipse.emf.common', version: '2.23.0'
    api group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.25.0'
    api group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi', version: '2.16.0'
    api group: 'org.eclipse.emfcloud', name: 'emfjson-jackson', version: '2.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation(libs.gson)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: "$kotlin_version"

    api "com.github.ajalt.clikt:clikt:$clikt_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.antlr:antlr4-runtime:$antlr_version"

}

generateTestGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'com.strumenta.simplelang']
    outputDirectory = new File("generated-test-src/antlr/main/com/strumenta/simplelang".toString())
}
compileKotlin.dependsOn generateGrammarSource
compileJava.dependsOn generateGrammarSource
compileTestKotlin.dependsOn generateTestGrammarSource

sourceSets {
    test {
        java {
            srcDirs += "generated-test-src/antlr/main"
        }
    }
}

compileKotlin.source sourceSets.main.java, sourceSets.main.kotlin
compileTestKotlin.source sourceSets.test.kotlin

clean {
    delete "generated-src"
    delete "generated-test-src"
}

compileJava.dependsOn generateTestGrammarSource

idea {
    module {
        testSourceDirs += file("generated-test-src/antlr/main")
    }
}

def isReleaseVersion = !version.toString().endsWith("SNAPSHOT")

publishing {

    repositories {
        maven {
            def releaseRepo = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://central.sonatype.com/repository/maven-snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('mavenCentralUsername') ? project.findProperty('mavenCentralUsername') : "Unknown user"
                password = project.hasProperty('mavenCentralPassword') ? project.findProperty('mavenCentralPassword') : "Unknown password"
            }
        }
    }

    publications {
        kolasu_emf(MavenPublication) {
            from components.java
            artifactId "kolasu-" + project.name
            artifact sourcesJar
            artifact javadocJar
            suppressPomMetadataWarningsFor('cliApiElements')
            suppressPomMetadataWarningsFor('cliRuntimeElements')
            pom {
                name = "kolasu-" + project.name
                description = "Framework to work with AST and building languages. Integrated with ANTLR."
                version = project.version
                packaging = 'jar'
                url = 'https://github.com/Strumenta/kolasu'

                scm {
                    connection = 'scm:git:https://github.com/Strumenta/kolasu.git'
                    developerConnection = 'scm:git:git@github.com:Strumenta/kolasu.git'
                    url = 'https://github.com/Strumenta/kolasu.git'
                }

                licenses {
                    license {
                        name = 'Apache Licenve V2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ftomassetti'
                        name = 'Federico Tomassetti'
                        email = 'federico@strumenta.com'
                    }
                    developer {
                        id = 'alessiostalla'
                        name = 'Alessio Stalla'
                        email = 'alessio.stalla@strumenta.com'
                    }
                    developer {
                        id = 'lorenzoaddazi'
                        name = 'Lorenzo Addazi'
                        email = 'lorenzo.addazi@strumenta.com'
                    }
                }

            }
        }
    }
}


signing {
    def key   = providers.gradleProperty('signingInMemoryKey').orNull
    def keyId = providers.gradleProperty('signingInMemoryKeyId').orNull
    def pass  = providers.gradleProperty('signingInMemoryKeyPassword').orNull

    if (key) {
        useInMemoryPgpKeys(keyId, key, pass)
        sign publishing.publications
    } else {
        logger.lifecycle('No in-memory signing key configured; skipping signing.')
    }
}

tasks.findByName("dokkaJavadoc").dependsOn(":core:compileKotlin")
tasks.findByName("sourcesJar").dependsOn("generateGrammarSource")
tasks.findByName("runKtlintCheckOverMainSourceSet").dependsOn("generateGrammarSource")
tasks.findByName("runKtlintCheckOverTestSourceSet").dependsOn("generateTestGrammarSource")
tasks.findByName("runKtlintFormatOverTestSourceSet").dependsOn("generateTestGrammarSource")
