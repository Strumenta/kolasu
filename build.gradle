plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
    id("maven-publish")
    id("idea")
    id("signing")
    id("org.jetbrains.dokka") version "1.4.32"
}

allprojects {
    project.group = "com.strumenta.kolasu"
    project.version = kolasu_version

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            afterSuite { desc, result ->
                println("desc ${desc}")
                println("result ${result}")
                if (!desc.parent) { // will match the outermost suite
                    println "------------------------------------------------------------------------------------"
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    println "------------------------------------------------------------------------------------"
                }
            }
            events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        }
        outputs.upToDateWhen {false}
    }

    ktlint {
        version = "0.41.0"
        verbose = true
        outputToConsole = true
        enableExperimentalRules = true
        disabledRules = ["no-wildcard-imports", "experimental:argument-list-wrapping"]
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }

    ktlint {
        disabledRules = ["no-wildcard-imports", "experimental:argument-list-wrapping"]
    }

}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
