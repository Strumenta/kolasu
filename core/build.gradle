plugins {
    id 'org.jetbrains.kotlin.jvm'
    alias(libs.plugins.ktlint)
    id "maven-publish"
    id "antlr"
    id "idea"
    id "signing"
    id "org.jetbrains.dokka"
}

java {
    sourceCompatibility = "$jvm_version"
    targetCompatibility = "$jvm_version"
    registerFeature('cli') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    antlr "org.antlr:antlr4:$antlr_version"
    implementation "org.antlr:antlr4-runtime:$antlr_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // we define some testing utilities
    implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    implementation 'org.redundent:kotlin-xml-builder:1.7.3'
    implementation("com.github.doyaaaaaken:kotlin-csv-jvm:1.9.2")
    implementation "com.google.code.gson:gson:$gson_version"

    api "com.github.ajalt.clikt:clikt:$clikt_version"

    implementation("com.google.code.gson:gson:$gson_version")

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    api(libs.lionwebjava)
    api(libs.lionwebkotlincore) {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-test-junit5"
    }
}

generateTestGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'com.strumenta.simplelang']
    outputDirectory = new File("generated-test-src/antlr/main/com/strumenta/simplelang".toString())
}
runKtlintCheckOverMainSourceSet.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource
compileJava.dependsOn generateGrammarSource
compileTestKotlin.dependsOn generateTestGrammarSource

sourceSets {
    test {
        java {
            srcDirs += "generated-test-src/antlr/main"
        }
    }
}

compileKotlin.source sourceSets.main.java, sourceSets.main.kotlin
compileTestKotlin.source sourceSets.test.kotlin

clean {
    delete "generated-src"
    delete "generated-test-src"
}

compileJava.dependsOn generateTestGrammarSource

idea {
    module {
        testSourceDirs += file("generated-test-src/antlr/main")
    }
}

publishing {

    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhTokenUsername') ? ossrhTokenUsername : "Unknown user"
                password = project.hasProperty('ossrhTokenPassword') ? ossrhTokenPassword : "Unknown password"
            }
        }
    }

    publications {
        kolasu_core(MavenPublication) {
            from components.java
            artifactId "kolasu-" + project.name
            artifact sourcesJar
            artifact javadocJar
            suppressPomMetadataWarningsFor('cliApiElements')
            suppressPomMetadataWarningsFor('cliRuntimeElements')
            pom {
                name = "kolasu-" + project.name
                description = "Framework to work with AST and building languages. Integrated with ANTLR."
                version = project.version
                packaging = 'jar'
                url = 'https://github.com/Strumenta/kolasu'

                scm {
                    connection = 'scm:git:https://github.com/Strumenta/kolasu.git'
                    developerConnection = 'scm:git:git@github.com:Strumenta/kolasu.git'
                    url = 'https://github.com/Strumenta/kolasu.git'
                }

                licenses {
                    license {
                        name = 'Apache Licenve V2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'ftomassetti'
                        name = 'Federico Tomassetti'
                        email = 'federico@strumenta.com'
                    }
                    developer {
                        id = 'alessiostalla'
                        name = 'Alessio Stalla'
                        email = 'alessio.stalla@strumenta.com'
                    }
                    developer {
                        id = 'lorenzoaddazi'
                        name = 'Lorenzo Addazi'
                        email = 'lorenzo.addazi@strumenta.com'
                    }
                }

            }
        }
    }
}

signing {
    sign publishing.publications.kolasu_core
}

tasks.findByName("generateGrammarSource").dependsOn("sourcesJar")
tasks.findByName("runKtlintCheckOverTestSourceSet").dependsOn("generateTestGrammarSource")
tasks.findByName("runKtlintFormatOverTestSourceSet").dependsOn("generateTestGrammarSource")
